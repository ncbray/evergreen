struct TextMatch {
}

struct RuneFilter {
  Min rune
  Max rune
}

struct RuneRangeMatch implements TextMatch {
  Invert bool
  Filters []RuneFilter
}

struct StringLiteralMatch implements TextMatch {
  Value string
}

struct MatchSequence implements TextMatch {
  Matches []TextMatch
}

struct MatchChoice implements TextMatch {
  Matches []TextMatch
}

struct MatchRepeat implements TextMatch {
  Match TextMatch
  Min int
}

struct MatchLookahead implements TextMatch {
  Invert bool
  Match TextMatch
}

struct ASTExpr {
}

struct RuneLiteral implements ASTExpr {
  Text string
  Value rune
}

struct StringLiteral implements ASTExpr {
  Text string
  Value string
}

struct IntLiteral implements ASTExpr {
  Text string
  Value int
}

struct BoolLiteral implements ASTExpr {
  Text string
  Value bool
}

func S () {
  /[ \t\r\n]*/;
}

func EndKeyword () {
  /![a-zA-Z_0-9]/;
  S();
}

func Ident (string) {
  text := /[a-zA-Z_][a-zA-Z_0-9]*/;
  S();
  return text;
}

func DecodeInt (int) {
    var value int;
    plus {
      digit := - coerce int $[0-9] coerce int '0';
      value = + * value 10 digit;
    }
    return value;
}

func EscapedChar (rune) {
  choose {
    /[a]/;
    return '\a';
  } or {
    /[b]/;
    return '\b';
  } or {
    /[f]/;
    return '\f';
  } or {
    /[n]/;
    return '\n';
  } or {
    /[r]/;
    return '\r';
  } or {
    /[t]/;
    return '\t';
  } or {
    /[v]/;
    return '\v';
  } or {
    /[\\]/;
    return '\\';
  } or {
    /[']/;
    return '\'';
  } or {
    /["]/;
    return '"';
  }
}

func DecodeString (string) {
  /["]/;
  contents := []rune{}
  star {
    choose {
      append contents $[^"\\];
    } or {
      /[\\]/;
      append contents EscapedChar();
    }
  }
  /["]/;
  return coerce string contents;
}

func DecodeRune (rune) {
  var value rune;
  /[']/;
  choose {
    value = $[^\\'];
  } or {
    /[\\]/;
    value = EscapedChar();
  }
  /[']/;
  return value;
}

func DecodeBool (bool) {
  choose {
    /"true"/;
    return true;
  } or {
    /"false"/;
    return false;
  }
}

func Literal (ASTExpr) {
  choose {
    var value rune;
    text := slice {
      value = DecodeRune();
    }
    S();
    return RuneLiteral{Text: text, Value: value};
  } or {
    var value string;
    text := slice {
      value = DecodeString();
    }
    S();
    return StringLiteral{Text: text, Value: value};
  } or {
    var value int;
    text := slice {
      value = DecodeInt();
    }
    S();
    return IntLiteral{Text: text, Value: value};
  } or {
    var value bool;
    text := slice {
      value = DecodeBool();
    }
    EndKeyword();
    return BoolLiteral{Text: text, Value: value};
  }
}

func BinaryOperator (string) {
  op := /[+\-*/]|([<>][=]?)|([!=][=]) ![+\-*/<>!=]/;
  S();
  return op;
}


struct StringMatch implements ASTExpr {
   Match TextMatch
}

struct RuneMatch implements ASTExpr {
   Match RuneRangeMatch
}

func StringMatchExpr (StringMatch) {
  /[/]/;
  S();
  e := ParseMatchChoice();
  /[/]/;
  S();
  return StringMatch{Match: e};
}

func RuneMatchExpr (RuneMatch) {
  /[$]/;
  S();
  e := MatchRune();
  return RuneMatch{Match: e};
}


test Ident Alpha "a_B_c "
  "a_B_c"

test Ident AlphaNum "a_1_7  "
  "a_1_7"

test Literal Num "1234567890  "
  IntLiteral{
    Text: "1234567890"
    Value: 1234567890
 }

test Literal Str "\"hello, world\\n\"  "
  StringLiteral{
    Text: "\"hello, world\\n\""
    Value: "hello, world\n"
  }

test Literal Rune "'a'  "
  RuneLiteral{
    Text: "'a'"
    Value: 'a'
  }

test Literal EscapedRune "'\\n'  "
  RuneLiteral{
    Text: "'\\n'"
    Value: '\n'
  }

struct ASTDecl {
}

struct ASTType implements ASTDecl {
}

struct ASTTypeRef {
}

struct TypeRef implements ASTTypeRef {
  Name string
  T ASTType
}

struct ListTypeRef implements ASTTypeRef {
  Type ASTTypeRef
  T ASTType
}

func ParseStructTypeRef (TypeRef) {
    return TypeRef{Name: Ident()};
}

func ParseListTypeRef (ListTypeRef) {
    /[\[][\]]/;
    return ListTypeRef{Type: ParseTypeRef()};
}


func ParseTypeRef (ASTTypeRef) {
  choose {
    return ParseStructTypeRef();
  } or {
    return ParseListTypeRef();
  }
}

test ParseTypeRef ListOfList "[][]Foo"
  ListTypeRef{
    Type: ListTypeRef{
      Type: TypeRef{
        Name: "Foo"
      }
    }
  }

struct Destructure {
}

struct DestructureValue implements Destructure {
  Expr ASTExpr
}

struct DestructureField {
  Name string
  Destructure Destructure
}

struct DestructureStruct implements Destructure {
  Type TypeRef
  Args []DestructureField
}

struct DestructureList implements Destructure {
  Type ListTypeRef
  Args []Destructure
}

func ParseDestructure (Destructure) {
  choose {
    t := ParseStructTypeRef();
    /[{]/;
    S();
    fields := []DestructureField{};
    star {
      name := Ident();
      /[:]/;
      S();
      d := ParseDestructure();
      append fields DestructureField{Name: name, Destructure: d};
    }
    /[}]/;
    S();
    return DestructureStruct{Type: t, Args: fields};
  } or {
    t := ParseListTypeRef();
    /[{]/;
    S();
    fields := []Destructure{};
    star {
      append fields ParseDestructure();
    }
    /[}]/;
    S();
    return DestructureList{Type: t, Args: fields};
  } or {
    return DestructureValue{Expr: Literal()}
  }
}

test ParseDestructure Compound "[]Foo { Bar { Baz: 1 Biz : 'x'} } "
  DestructureList {
    Type: ListTypeRef {
      Type: TypeRef {
        Name: "Foo"
      }
    }
    Args: []Destructure {
      DestructureStruct {
        Type: TypeRef {
          Name: "Bar"
        }
        Args: []DestructureField {
          DestructureField {
            Name: "Baz"
            Destructure: DestructureValue {
              Expr: IntLiteral {
                Value: 1
              }
            }
          }
          DestructureField {
            Name: "Biz"
            Destructure: DestructureValue {
              Expr: RuneLiteral {
                Value: 'x'
              }
            }
          }
        }
      }
    }
  }

func ParseRuneFilterRune (rune) {
  choose {
    return $[^\]\-\\];
  } or {
    /[\\]/;
    choose {
      return EscapedChar();
    } or {
      return $[^];
    }
  }
}

func ParseRuneFilter (RuneFilter) {
  min := ParseRuneFilterRune();
  max := min;
  question {
    /[\-]/;
    max = ParseRuneFilterRune();
  }
  return RuneFilter{Min: min, Max: max};
}

func MatchRune (RuneRangeMatch) {
  /[\[]/;
  invert := false;
  filters := []RuneFilter{};
  question {
    /[\^]/;
    invert = true;
  }
  star {
    append filters ParseRuneFilter();
  }
  /[\]]/;
  S();
  return RuneRangeMatch{Invert: invert, Filters: filters};
}

func Atom (TextMatch) {
  choose {
    return MatchRune();
  } or {
    value := DecodeString();
    S();
    return StringLiteralMatch{Value: value};
  } or {
    /[(]/;
    S();
    e := ParseMatchChoice();
    /[)]/;
    S();
    return e;
  }
}

func MatchPostfix (TextMatch) {
  e := Atom();
  choose {
    /[*]/;
    S();
    return MatchRepeat{Match: e, Min: 0};
  } or {
    /[+]/;
    S();
    return MatchRepeat{Match: e, Min: 1};
  } or {
    /[?]/;
    S();
    return MatchChoice{Matches: []TextMatch{e, MatchSequence{Matches: []TextMatch{}}}};
  } or {
    return e;
  }
}

func MatchPrefix (TextMatch) {
  choose {
    var invert bool;
    choose {
      /[!]/;
      invert = true;
    } or {
      /[&]/;
    }
    S();
    return MatchLookahead{Invert: invert, Match: MatchPostfix()};
  } or {
    return MatchPostfix();
  }
}

func Sequence (TextMatch) {
  e := MatchPrefix();
  choose {
    l := []TextMatch{e}
    plus {
      append l MatchPrefix();
    }
    return MatchSequence{Matches: l};
  } or {
    return e;
  }
}

func ParseMatchChoice (TextMatch) {
  e := Sequence();
  choose {
    l := []TextMatch{e};
    plus {
      /[|]/;
      S();
      append l Sequence();
    }
    return MatchChoice{Matches: l};
  } or {
    return e;
  }
}


test ParseMatchChoice One "[pq]"
  RuneRangeMatch {
    Invert: false
    Filters: []RuneFilter {
      RuneFilter {
        Min: 'p'
        Max: 'p'
      }
      RuneFilter {
        Min: 'q'
        Max: 'q'
      }
    }
  }

test ParseMatchChoice Invert "[^.]"
  RuneRangeMatch {
    Invert: true
    Filters: []RuneFilter {
      RuneFilter {
        Min: '.'
        Max: '.'
      }
    }
  }

test ParseMatchChoice Escape "[\\]\\n]"
  RuneRangeMatch {
    Filters: []RuneFilter {
      RuneFilter {
        Min: ']'
        Max: ']'
      }
      RuneFilter {
        Min: '\n'
        Max: '\n'
      }
    }
  }


test ParseMatchChoice Three "[a][b][c]"
  MatchSequence {
    Matches: []TextMatch {
      RuneRangeMatch {
        Filters: []RuneFilter {
          RuneFilter {
            Min: 'a'
            Max: 'a'
          }
        }
      }
      RuneRangeMatch {
        Filters: []RuneFilter {
          RuneFilter {
            Min: 'b'
            Max: 'b'
          }
        }
      }
      RuneRangeMatch {
        Filters: []RuneFilter {
          RuneFilter {
            Min: 'c'
            Max: 'c'
          }
        }
      }
    }
  }

test ParseMatchChoice Repeat "[0-9] + "
  MatchRepeat {
    Match: RuneRangeMatch {
      Filters: []RuneFilter {
        RuneFilter {
          Min: '0'
          Max: '9'
        }
      }
    }
    Min: 1
  }

test ParseMatchChoice Question "[*]?"
  MatchChoice {
    Matches: []TextMatch {
      RuneRangeMatch {
        Filters: []RuneFilter {
          RuneFilter {
            Min: '*'
            Max: '*'
          }
        }
      }
      MatchSequence {
        Matches: []TextMatch{}
      }
    }
  }

test ParseMatchChoice Complex "[a]([b]|[c])|[d]"
  MatchChoice {
    Matches: []TextMatch {
      MatchSequence {
        Matches: []TextMatch{
          RuneRangeMatch {
            Filters: []RuneFilter {
              RuneFilter {
                Min: 'a'
                Max: 'a'
               }
            }
          }
          MatchChoice {
            Matches: []TextMatch {
              RuneRangeMatch {
                Filters: []RuneFilter {
                  RuneFilter {
                    Min: 'b'
                    Max: 'b'
                  }
                }
              }
              RuneRangeMatch {
                Filters: []RuneFilter {
                  RuneFilter {
                    Min: 'c'
                    Max: 'c'
                  }
                }
              }
            }
          }
        }
      }
      RuneRangeMatch {
        Filters: []RuneFilter {
          RuneFilter {
            Min: 'd'
            Max: 'd'
          }
        }
      }
    }
  }

test ParseMatchChoice PositiveLookahead "&[.]"
  MatchLookahead {
    Invert: false
    Match: RuneRangeMatch {
      Invert: false
      Filters: []RuneFilter {
        RuneFilter {
          Min: '.'
          Max: '.'
        }
      }
    }
  }

test ParseMatchChoice NegativeLookahead "![.]"
  MatchLookahead {
    Invert: true
    Match: RuneRangeMatch {
      Invert: false
      Filters: []RuneFilter {
        RuneFilter {
          Min: '.'
          Max: '.'
        }
      }
    }
  }


struct If implements ASTExpr {
  Expr ASTExpr
  Block []ASTExpr
}

struct Repeat implements ASTExpr {
  Block []ASTExpr
  Min int
}

struct Choice implements ASTExpr {
  Blocks [][]ASTExpr
}

struct Optional implements ASTExpr {
  Block []ASTExpr
}

struct Slice implements ASTExpr {
  Block []ASTExpr
}

struct Assign implements ASTExpr {
  Expr ASTExpr
  Name string
  Info int
  Type ASTTypeRef
  Define bool
}

struct GetName implements ASTExpr {
  Name string
  Info int
}

struct NamedExpr {
  Name string
  Expr ASTExpr
}

struct Construct implements ASTExpr {
  Type TypeRef
  Args []NamedExpr
}

struct ConstructList implements ASTExpr {
  Type ListTypeRef
  Args []ASTExpr
}

struct Coerce implements ASTExpr {
  Type ASTTypeRef
  Expr ASTExpr
}

struct Call implements ASTExpr {
  Name string
  T ASTType
}

struct Fail implements ASTExpr {
}

struct Append implements ASTExpr {
  List ASTExpr
  Expr ASTExpr
  T ASTType
}

struct Return implements ASTExpr {
  Exprs []ASTExpr
}

struct BinaryOp implements ASTExpr {
  Left ASTExpr
  Op string
  Right ASTExpr
  T ASTType
}

func ParseExprList ([]ASTExpr) {
  exprs := []ASTExpr{}
  question {
    append exprs ParseExpr();
    star {
      /[,]/;
      S();
      append exprs ParseExpr();
    }
  }
  return exprs;
}

func ParseNamedExpr (NamedExpr) {
  name := Ident();
  /[:]/;
  S();
  return NamedExpr{Name: name, Expr: ParseExpr()};
}

func ParseNamedExprList ([]NamedExpr) {
  exprs := []NamedExpr{}
  question {
    append exprs ParseNamedExpr();
    star {
      /[,]/;
      S();
      append exprs ParseNamedExpr();
    }
  }
  return exprs;
}


func ParseTypeList ([]ASTTypeRef) {
  /[(]/;
  S();
  types := []ASTTypeRef{}
  question {
    append types ParseTypeRef();
    star {
      /[,]/;
      S();
      append types ParseTypeRef();
    }
  }
  /[)]/;
  S();
  return types;
}

func ParseExpr (ASTExpr) {
  choose {
    return Literal();
  } or {
    var count int
    choose {
      /"star"/;
    } or {
      /"plus"/;
      count = 1;
    }
    EndKeyword();
    block := ParseCodeBlock();
    return Repeat{Block: block, Min: count};
  } or {
    /"choose"/;
    EndKeyword();
    blocks := [][]ASTExpr{ParseCodeBlock()}
    star {
      /"or"/;
      EndKeyword();
      append blocks ParseCodeBlock();
    }
    return Choice{Blocks: blocks};
  } or {
    /"question"/;
    EndKeyword();
    block := ParseCodeBlock();
    return Optional{Block: block};
  } or {
    /"slice"/;
    EndKeyword();
    block := ParseCodeBlock();
    return Slice{Block: block};
  } or {
    /"if"/;
    EndKeyword();
    expr := ParseExpr();
    block := ParseCodeBlock();
    return If{Expr: expr, Block: block};
  } or {
    /"var"/;
    EndKeyword();
    name := Ident();
    t := ParseTypeRef();
    var expr ASTExpr;
    question {
      /[=]/;
      S();
      expr = ParseExpr(); 
    }
    return Assign{Expr: expr, Name: name, Type: t, Define: true};
  } or {
    /"fail"/;
    EndKeyword();
    return Fail{};
  } or {
    /"coerce"/;
    S();
    t := ParseTypeRef();
    e := ParseExpr();
    return Coerce{Type: t, Expr: e};
  } or {
    /"append"/;
    EndKeyword();
    name := Ident();
    expr := ParseExpr();
    return Assign{
      Expr: Append{
        List: GetName{Name: name},
        Expr: expr
      },
      Name: name
    };
  } or {
    /"return"/;
    EndKeyword();
    choose {
      /[(]/;
      S();
      exprs := ParseExprList();
      /[)]/;
      S();
      return Return{Exprs: exprs};
    } or {
      return Return{Exprs: []ASTExpr{ParseExpr()}}
    } or {
      return Return{Exprs: []ASTExpr{}}
    }
  } or {
    name := Ident();
    /[(]/;
    S();
    /[)]/;
    S();  
    return Call{Name: name};
  } or {
    op := BinaryOperator();
    l := ParseExpr();
    r := ParseExpr();
    return BinaryOp{Left: l, Op: op, Right: r};
  } or {
    t := ParseStructTypeRef();
    /[{]/;
    S();
    args := ParseNamedExprList();
    /[}]/;
    S();
    return Construct{Type: t, Args: args};
  } or {
    t := ParseListTypeRef();
    /[{]/;
    S();
    args := ParseExprList();
    /[}]/;
    S();
    return ConstructList{Type: t, Args: args};
  } or {
    return StringMatchExpr();
  } or {
    return RuneMatchExpr();
  } or {
    name := Ident();
    choose {
      var defined bool;
      choose {
        /":="/;
        defined = true;
      } or {
        /"="/;
      }
      S();
      expr := ParseExpr();
      return Assign{Expr: expr, Name: name, Define: defined};
    } or {
      return GetName{Name: name};
    }
  }
}

func ParseCodeBlock([]ASTExpr) {
  /[{]/;
  S();
  exprs := []ASTExpr{};
  star {
    append exprs ParseExpr();
    star {
      /[;]/;
      S();
    }
  }
  /[}]/;
  S();
  return exprs;
}

test ParseExpr BinaryOp "+ 1 2"
  BinaryOp {
    Left: IntLiteral {
      Value: 1
    }
    Op: "+"
    Right: IntLiteral {
      Value: 2
    }
  }

test ParseExpr Assign "foo = 1"
  Assign {
    Name: "foo"
    Expr: IntLiteral {
      Value: 1
    }
    Define: false
  }

test ParseExpr Lookahead "returnType = 1"
  Assign {
    Name: "returnType"
    Expr: IntLiteral {
      Value: 1
    }
    Define: false
  }

test ParseExpr Define "foo := 1"
  Assign {
    Name: "foo"
    Expr: IntLiteral {
      Value: 1
    }
    Define: true
  }

test ParseExpr Construct "Foo{ Bar: 1} "
  Construct {
    Type: TypeRef {
      Name: "Foo"
    }
    Args: []NamedExpr {
      NamedExpr{
        Name: "Bar"
        Expr: IntLiteral {
          Value: 1
        }
      }
    }
  }

test ParseExpr ConstructList "[]Foo{ 1 } "
  ConstructList {
    Type: ListTypeRef{
      Type: TypeRef {
        Name: "Foo"
      }
    }
    Args: []ASTExpr {
      IntLiteral {
        Value: 1
      }
    }
  }


struct BuiltinType implements ASTType {
  Name string
}

struct ListType implements ASTType {
  Type ASTType
}

struct FieldDecl {
  Name string
  Type ASTTypeRef
}

struct StructDecl implements ASTType {
  Name string
  Implements ASTTypeRef
  Fields []FieldDecl
}


func ParseStructDecl (StructDecl) {
  /"struct"/;
  EndKeyword();
  name := Ident();
  var implements ASTTypeRef;
  question {
    /"implements"/;
    EndKeyword();
    implements = ParseTypeRef();
  }
  /[{]/;
  S();
  fields := []FieldDecl{}
  star {
    append fields FieldDecl{Name: Ident(), Type: ParseTypeRef()}
  }
  /[}]/;
  S();
  return StructDecl{Name: name, Implements: implements, Fields: fields};
}

struct ASTFunc implements ASTDecl {
}

struct LocalInfo {
  Name string
  T ASTType
}

struct FuncDecl implements ASTFunc {
  Name string
  ReturnTypes []ASTTypeRef
  Block []ASTExpr
  Locals []LocalInfo
}

func ParseFuncDecl (FuncDecl) {
  /"func"/;
  EndKeyword();
  name := Ident();
  retTypes := ParseTypeList();
  block := ParseCodeBlock();
  return FuncDecl{Name: name, ReturnTypes: retTypes, Block: block}
}

struct Test {
  Rule string
  Name string
  Type ASTType
  Input string
  Destructure Destructure
}

func ParseTest (Test) {
  /"test"/;
  EndKeyword();
  rule := Ident();
  name := Ident();
  input := DecodeString();
  S();
  d := ParseDestructure();
  return Test{Rule: rule, Name: name, Input: input, Destructure: d};
}

struct File {
  Decls []ASTDecl
  Tests []Test
}

func ParseFile (File) {
  decls := []ASTDecl{};
  tests := []Test{};
  star {
    choose {
      append decls ParseFuncDecl();
    } or {
      append decls ParseStructDecl();
    } or {
      append tests ParseTest();
    }
  }
  /![^]/;
  return File{Decls: decls, Tests: tests};
}
