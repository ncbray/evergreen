func EOL() {
  /"\n"|"\r\n"|"\r"/
}

func SingleLineComment() {
  /"//" [^\n\r]*/
}

func S() {
  star {
    choose {
      /[ \t]/
    } or {
      EOL()
    } or {
      SingleLineComment()
    }
  }
}

func EOSInsertionPoint() {
  /[ \t]*/
}

func EOS() {
  choose {
    S()
    /[;]|&[)}]|![^]/
  } or {
    question {
      SingleLineComment()
    }
    EOL()
  }
}

func EndKeyword() {
  /![a-zA-Z_0-9]/
}

func NotReserved() {
  /!(("type"|"struct") ![a-zA-Z_0-9])/
}

func Id() Token {
  p := position()
  NotReserved();
  text := /[a-zA-Z_][a-zA-Z_0-9]*/
  return Token{Pos: p, Text: text}
}

func ParseNamedTypeRef() TypeRef {
  name := Id()
  return NamedTypeRef {
    Name: name
  }
}

func ParseListTypeRef() TypeRef {
  choose {
    /"[]"/;
    S();
    return ListTypeRef{Type: ParseListTypeRef()}
  } or {
    return ParseNamedTypeRef()
  }
}

func ParseSumTypeRef() TypeRef {
  t := ParseListTypeRef()
  choose {
    types := []TypeRef{t}
    plus {
      S()
      /[|]/
      S()
      types = append(types, ParseListTypeRef())
    }
    return SumTypeRef{Types: types}
  } or {
    return t
  }
}

func ParseTypeRef() TypeRef {
  return ParseSumTypeRef()
}

func ParseField() FieldDecl {
  name := Id()
  S()
  type := ParseTypeRef()
  EOS()
  return FieldDecl {
    Name: name,
    Type: type
  }
}

func ParseFields() []FieldDecl {
  fields := []FieldDecl{}
  star {
    fields = append(fields, ParseField())
    S()
  }
  return fields
}

func ParseStructDecl() StructDecl {
  /"struct"/
  EndKeyword()
  S()
  /[{]/
  S()
  fields := ParseFields()
  S()
  /[}]/
  return StructDecl {
    Fields: fields
  }
}

func ParseTypeAliasDecl() TypeAliasDecl {
  /"="/
  S()
  t := ParseTypeRef()
  S()
  /[;]/
  return TypeAliasDecl{Type: t}
}

func ParseTypeDecl() TypeDecl {
  /"type"/
  EndKeyword()
  S()
  name := Id()
  S()
  var impl TypeImpl
  choose {
    impl = ParseStructDecl()
  } or {
    impl = ParseTypeAliasDecl()
  }
  return TypeDecl {
    Name: name,
    Decl: impl
  }
}

func ParseNamedExpr() NamedExpr {
  name := Id()
  S()
  /":"/
  S()
  value := ParseExpr()
  return NamedExpr {
    Name: name,
    Value: value
  }
}

func ParseExprList() []Expr {
  exprs := []Expr{}
  question {
    exprs = append(exprs, ParseExpr())
    star {
      S();
      /","/
      S()
      exprs = append(exprs, ParseExpr())
    }
    question {
      S();
      /","/
    }
  }
  return exprs
}

func ParseNamedExprList() []NamedExpr {
  exprs := []NamedExpr{}
  question {
    exprs = append(exprs, ParseNamedExpr())
    star {
      S();
      /","/
      S()
      exprs = append(exprs, ParseNamedExpr())
    }
    question {
      S();
      /","/
    }
  }
  return exprs
}

func ParseExprAtom() Expr {
  p := position()
  choose {
    text := /[0-9]+/
    return IntLiteral {
      Pos: p,
      Text: text
    }
  } or {
    t := ParseTypeRef()
    S()
    /"{"/;
    S();
    choose {
      args := ParseNamedExprList()
      S();
      /"}"/;
      return CreateStruct {
        Type: t,
        Args: args
      }
    } or {
      args := ParseExprList()
      S();
      /"}"/;
      return CreateList {
        Type: t,
        Args: args
      }
    }
  } or {
    name := Id()
    return GetName {
      Name: name
    }
  } or {
    /"("/
    S()
    expr := ParseExpr()
    S()
    /")"/
    return expr
  }
}

func ParseExprPostfix() Expr {
  expr := ParseExprAtom()
  star {
    S()
    choose {
      /"."/
      S()
      attr := Id()
      expr = GetAttr{Expr: expr, Attr: attr}
    } or {
      /"["/
      S()
      index := ParseExpr()
      S()
      /"]"/
      expr = GetIndex{Expr: expr, Index: index}
    }
  }
  return expr
}

func InfixOperator() (Token, int) {
  p := position()
  var text string
  var prec int
  choose {
    text = /[*/%]/
    prec = 5
  } or {
    text = /[+\-]/
    prec = 4
  } or {
    text = /([<>][=]?)|([!=][=])/
    prec = 3
  }
  return Token{Pos: p, Text: text}, prec
}


func ParseBinaryOp(min_prec int) Expr {
  e := ParseExprPostfix()
  star {
    // TODO associativity
    S()
    op, prec := InfixOperator()
    if prec < min_prec {
      fail
    }
    S()
    r := ParseBinaryOp(prec + 1)
    e = InfixOp{
      Left: e,
      Op: op,
      Right: r
    }
  }
  return e
}

func ParseAssignOp() Token {
  p := position()
  op := /":="|"="/
  return Token {
    Pos: p,
    Text: op
  }
}

func ParseAssignExpr() Expr {
  e := ParseBinaryOp(1)
  question {
    S()
    op := ParseAssignOp()
    S()
    other := ParseBinaryOp(1)
    e = AssignOp{
      Target: e,
      Op: op,
      Value: other
    }
  }
  return e
}

func ParseExpr() Expr {
  return ParseAssignExpr();
}

func ParseStatement() Stmt {
  choose {
    /"return"/
    EndKeyword()
    EOSInsertionPoint()
    var expr Expr
    question {
      expr = ParseExpr()
      EOSInsertionPoint()
    }
    EOS()
    return Return{Expr: expr}
  } or {
    expr := ParseExpr()
    EOSInsertionPoint()
    EOS()
    return expr
  }
}

func ParseStatementList() []Stmt {
  stmts := []Stmt{}
  question {
    stmts = append(stmts, ParseStatement())
    star {
      S()
      stmts = append(stmts, ParseStatement())
    }
  }
  return stmts
}

func ParseTypeList() []TypeRef {
  types := []TypeRef{}
  question {
    types = append(types, ParseTypeRef())
    star {
      S()
      /[,]/
      S()
      types = append(types, ParseTypeRef())
    }
  }
  return types
}

func ParseReturnTypeList() []TypeRef {
  choose {
    /[(]/
    S()
    types := ParseTypeList()
    S()
    /[)]/
    return types
  } or {
    return []TypeRef{ParseTypeRef()}
  } or {
    return []TypeRef{}
  }
}

func ParseParameter() Parameter {
  name := Id()
  S()
  type := ParseTypeRef()
  return Parameter{Name: name, Type: type}
}

func ParseParameterList() []Parameter {
  params := []Parameter{}
  question {
    params = append(params, ParseParameter())
    star {
      S()
      /[,]/
      S()
      params = append(params, ParseParameter())
    }
  }
  return params
}

func ParseFuncDecl() FuncDecl {
  /"func"/
  EndKeyword()
  S()
  name := Id()
  S()
  /"("/
  S()
  params := ParseParameterList()
  S()
  /")"/
  S()
  retTypes := ParseReturnTypeList()
  S()
  /"{"/
  S()
  body := ParseStatementList()
  S()
  /"}"/
  return FuncDecl {
    Name: name,
    Parameters: params,
    ReturnTypes: retTypes,
    Body: body
  }
}